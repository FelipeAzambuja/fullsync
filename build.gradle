buildscript {
	repositories {
		jcenter()
	}
}

plugins {
	id 'com.github.ben-manes.versions' version '0.14.0'
	id 'com.diffplug.gradle.spotless' version '3.3.2'
}

repositories {
	jcenter()
}

apply plugin: 'com.diffplug.gradle.spotless'
apply plugin: 'distribution'

group = "net.sourceforge.fullsync"
version = '0.11.0'

spotless {
	encoding 'UTF-8'
}

subprojects {
	buildscript {
		repositories {
			jcenter()
		}
	}
	group = rootProject.group
	version = rootProject.version
	apply plugin: 'com.diffplug.gradle.spotless'
	apply plugin: 'jacoco'

	def subproject = delegate
	plugins.withType(JavaPlugin).whenPluginAdded { p ->
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
		jar {
			manifest {
				attributes 'License': 'GPLv2+'
				attributes 'FullSync-Version': version
				attributes 'Copyright': 'Copyright (c) 2017 the FullSync Team'
			}
		}
		spotless {
			java {
				licenseHeaderFile rootProject.file('resources/License-header.txt')
				eclipseFormatFile rootProject.file('resources/eclipse-jdt-formatter.xml')
				removeUnusedImports()
				trimTrailingWhitespace()
				endWithNewline()
			}
		}
		jacocoRootReport.dependsOn subproject.tasks.jacocoTestReport
	}

	ext {
		swtVersion = "3.105.3"
		slf4jVersion = "1.7.25"
		commonsCliVersion = "1.4"
		commonsNetVersion = "3.6"
		commonsVfsVersion = "2.1"
		sambaJcifsVersion = "1.3.17"
		jcraftJschVersion = "0.1.54"
		junitVersion = "4.12"
	}

	repositories {
		jcenter()
	}
}

wrapper {
	gradleVersion = '3.5'
}

dependencyUpdates {
	revision = "release"
	outputFormatter = "plain"
}

distributions {
	linux
	mac
	windows {
		contents {
			from('resources') {
				include 'fullsync*.exe'
				include 'fullsync*.ini'
			}
		}
	}
}

distZip.enabled = false
distTar.enabled = false
linuxDistZip.enabled = false
macDistZip.enabled = false
windowsDistTar.enabled = false

tasks.withType(Tar) {
	compression = Compression.GZIP
}

installLinuxDist.dependsOn ':fullsync-ui:jar'
installMacDist.dependsOn ':fullsync-ui:jar'
installWindowsDist.dependsOn ':fullsync-ui:jar'
linuxDistTar.dependsOn ':fullsync-ui:jar'
macDistTar.dependsOn ':fullsync-ui:jar'
windowsDistZip.dependsOn ':fullsync-ui:jar'

assembleDist {
	dependsOn assembleLinuxDist
	dependsOn assembleMacDist
	dependsOn assembleWindowsDist
}

def mapArtifactToFilename(a) {
	def group = a.moduleVersion.id.group
	def classifier = a.classifier ?: ""
	classifier = classifier ? "-${classifier}" : ""
	return "${group}-${a.name}${classifier}.${a.extension}"
}

gradle.projectsEvaluated {
	def ui = project(':fullsync-ui')
	def resolvedUiDist = ui.configurations.dist.resolvedConfiguration
	def commonContents = copySpec {}
	resolvedUiDist.resolvedArtifacts.each { a ->
		commonContents.with copySpec {
			def f = a.file
			into('lib') {
				from(f.path) {
					rename(f.name, mapArtifactToFilename(a))
				}
			}
		}
	}
	commonContents.with copySpec {
		into('lib') {
			from resolvedUiDist.files - resolvedUiDist.resolvedArtifacts*.file
		}
	}
	distributions.linux.contents.with commonContents
	distributions.mac.contents.with commonContents
	distributions.windows.contents.with commonContents

	subprojects.findAll { subproject ->
		subproject.pluginManager.hasPlugin('java')
	}.each { subproject ->
		jacocoRootReport.sourceDirectories = jacocoRootReport.sourceDirectories + subproject.jacocoTestReport.sourceDirectories
		jacocoRootReport.classDirectories = jacocoRootReport.classDirectories + subproject.jacocoTestReport.classDirectories
		jacocoRootReport.executionData = jacocoRootReport.executionData + subproject.jacocoTestReport.executionData
	}
}

apply plugin: 'jacoco'

jacoco {
	reportsDir = file("${project.reporting.baseDir}/jacoco")
}

task jacocoRootReport(type: JacocoReport) {
	sourceDirectories = files()
	classDirectories = files()
	executionData = files()
	reports {
		html.enabled = true
		xml.enabled = true
		csv.enabled = false
	}

	// Work-around to allow us to build list of executionData files in doFirst
	onlyIf = {
		true
	}

	doFirst {
		executionData = files(executionData.findAll {
			it.exists()
		})
	}
}

task run(type: JavaExec) {
	dependsOn installLinuxDist
	main = '-jar'
	args = ["${buildDir}/install/${project.name}-linux/lib/net.sourceforge.fullsync-fullsync.jar", "-v"]
}

task debug(type: JavaExec) {
	dependsOn installLinuxDist
	main = '-jar'
	args = ["${buildDir}/install/${project.name}-linux/lib/net.sourceforge.fullsync-fullsync.jar", "-v"]
	debug = true
	enableAssertions = true
}
