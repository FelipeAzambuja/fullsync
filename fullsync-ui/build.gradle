
apply plugin: 'java'

def getSWTVariant(osName, osArch) {
	def arch = osArch.contains('64') ? 'x86_64' : 'x86'
	def os = ''
	if (osName.contains('windows')) {
		os = 'win32.win32'
	}
	else if (osName.contains('linux')) {
		os = 'gtk.linux'
	}
	else if (osName.contains('mac')) {
		os = 'cocoa.macosx'
	}
	return "org.eclipse.platform:org.eclipse.swt.${os}.${arch}:${ext.swtVersion}"
}

def getLocalSWTVersion() {
	return getSWTVariant(System.properties['os.name'].toLowerCase(), System.properties['os.arch'])
}

configurations {
	dist.extendsFrom(runtime)
}

dependencies {
	compile project(':fullsync-core')
	compile project(':fullsync-assets')
	compile getLocalSWTVersion()
	dist getSWTVariant("linux", "x86")
	dist getSWTVariant("linux", "x86_64")
	dist getSWTVariant("mac", "x86_64")
	dist getSWTVariant("windows", "x86")
	dist getSWTVariant("windows", "x86_64")
	dist project(':fullsync-ui')
}

configurations {
	all*.exclude group: 'org.eclipse.platform', module: 'org.eclipse.swt'
}

task run(type: JavaExec) {
	main = 'net.sourceforge.fullsync.ui.DebugMain'
	classpath = configurations.runtime + sourceSets.main.output
}
